/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

subprojects {
    apply plugin: "maven-publish"

    publishing {
        if (!project.name.endsWith("-integration")) {
            publications {
                shadow(MavenPublication) { publication ->
                    if (project.name.endsWith("-runtime")) {
                        project.shadow.component(publication)
                    } else {
                        from components.java
                    }
                    artifactId = archivesBaseName
                    pom {
                        name = "Spark ClickHouse Connector"
                        url = "https://github.com/pan3793/spark-clickhouse-connector"
                        licenses {
                            license {
                                name = "The Apache License, Version 2.0"
                                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                                distribution = "repo"
                            }
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                String snapshotsRepoUrl = project.hasProperty("mavenSnapshotsRepo") ? "$mavenSnapshotsRepo"
                        : "https://oss.sonatype.org/content/repositories/snapshots"
                String releasesRepoUrl = project.hasProperty("mavenReleasesRepo") ? "$mavenReleasesRepo"
                        : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                url = project.version.endsWith("-SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = project.hasProperty("mavenUser") ? "$mavenUser" : ""
                    password = project.hasProperty("mavenPassword") ? "$mavenPassword" : ""
                }
            }
        }
    }
}
