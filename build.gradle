plugins {
    id "antlr"
    id "idea"
    id "scala"
    id "java-library"
    id "maven-publish"
    id "org.scoverage" version "5.0.0"
    id "com.google.protobuf" version "0.8.16"
    id "com.github.maiflai.scalatest" version "0.31"
}

repositories {
    String mavenRepoMirror = project.hasProperty('mavenRepoMirror') ? "$mavenRepoMirror"
            : "https://maven.aliyun.com/repository/central"
    maven { url = mavenRepoMirror }
}

configurations.all {
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"

    // use hadoop shaded client instead
    exclude group: "org.apache.hadoop", module: "hadoop-client"
    exclude group: "org.apache.hadoop", module: "hadoop-common"

    resolutionStrategy {
        force "org.scala-lang:scala-compiler:$scala_version"
        force "org.scala-lang:scala-library:$scala_version"
        force "org.scala-lang:scala-reflect:$scala_version"
    }
}

configurations {
    runtimeClasspath {
        // workaround for https://github.com/gradle/gradle/issues/820
        exclude group: "org.antlr", module: "antlr4"
    }
}

dependencies {
    api "org.slf4j:slf4j-api:$slf4j_version"

    api "io.grpc:grpc-stub:$grpc_version"
    api "io.grpc:grpc-protobuf:$grpc_version"
    api "io.grpc:grpc-netty-shaded:$grpc_version"

    antlr "org.antlr:antlr4:$antlr_version"

    compileOnlyApi "org.scala-lang:scala-library:$scala_version"
    compileOnlyApi "org.scala-lang:scala-reflect:$scala_version"

    compileOnlyApi "org.apache.hadoop:hadoop-client-api:$hadoop_version"

    compileOnlyApi "org.apache.spark:spark-core_$scala_binary_version:$spark_version"
    compileOnlyApi "org.apache.spark:spark-sql_$scala_binary_version:$spark_version"

    compileOnlyApi "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    compileOnlyApi "com.fasterxml.jackson.module:jackson-module-scala_$scala_binary_version:$jackson_version"

    testImplementation "org.scala-lang:scala-library:$scala_version"
    testImplementation "org.scala-lang:scala-reflect:$scala_version"

    testImplementation "org.apache.spark:spark-core_$scala_binary_version:$spark_version"
    testImplementation "org.apache.spark:spark-sql_$scala_binary_version:$spark_version"

    testImplementation "org.apache.hadoop:hadoop-client-api:$hadoop_version"
    testRuntimeOnly "org.apache.hadoop:hadoop-client-runtime:$hadoop_version"
    // required when using Hadoop Shaded Client, see SPARK-33212
    testRuntimeOnly "commons-collections:commons-collections:$commons_collections_version"
    testRuntimeOnly "commons-io:commons-io:$commons_io_version"

    testImplementation "org.scalatest:scalatest_$scala_binary_version:$scalatest_version"
    testRuntimeOnly "com.vladsch.flexmark:flexmark-all:$flexmark_version"

    testImplementation "com.dimafeng:testcontainers-scala-scalatest_$scala_binary_version:$testcontainers_scala_version"
    testImplementation "com.dimafeng:testcontainers-scala-clickhouse_$scala_binary_version:$testcontainers_scala_version"
    testRuntimeOnly "ru.yandex.clickhouse:clickhouse-jdbc:$clickhouse_jdbc_version"

    testRuntimeOnly "org.slf4j:log4j-over-slf4j:$slf4j_version"
    testRuntimeOnly "ch.qos.logback:logback-classic:$logback_version"
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-package", "xenon.clickhouse"]
}

sourceSets.main.scala {
    srcDirs += "src/main/scala-$scala_binary_version"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

compileScala {
    options.fork = true
    options.forkOptions.jvmArgs += ["-Xss8M"]
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

test {
    maxParallelForks = 1
}

scoverage {
    reportDir.set(file("${rootProject.buildDir}/reports/scoverage"))
    highlighting.set(false)
    minimumRate.set(0.0)
}

version = getProjectVersion()

boolean isVersionFileExists() {
    return file("version.txt").exists()
}

String getVersionFromFile() {
    return file("version.txt").text.trim()
}

String getProjectVersion() {
    if (isVersionFileExists())
        return getVersionFromFile()

    String baseVersion = new Date().format("yyyy.MM.dd")
    return project.hasProperty("release") ? baseVersion : "$baseVersion-SNAPSHOT"
}

publishing {
    publications {
        lib(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            String snapshotsRepoUrl = project.hasProperty('mavenSnapshotsRepo') ? "$mavenSnapshotsRepo"
                    : "https://oss.sonatype.org/content/repositories/snapshots"
            String releasesRepoUrl = project.hasProperty('mavenReleasesRepo') ? "$mavenReleasesRepo"
                    : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url = project.version.endsWith('-SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.hasProperty('mavenUser') ? "$mavenUser" : ""
                password = project.hasProperty('mavenPassword') ? "$mavenPassword" : ""
            }
        }
    }
}
